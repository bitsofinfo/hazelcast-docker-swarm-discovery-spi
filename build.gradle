buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group = 'org.bitsofinfo'

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'

    mainClassName = 'org.bitsofinfo.hazelcast.discovery.docker.swarm.test.DockerTestRunner'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile group: 'com.hazelcast', name: 'hazelcast', version: '[3.9,3.10-BETA-1)'
    compile group: 'com.spotify', name: 'docker-client', version: '[8,9)', classifier: 'shaded'
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('bintrayUser')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('bintrayApiKey')
    publications = ['hazelcastDockerSwarmDiscoverySpi']
    pkg {
        repo = 'maven'
        name = 'hazelcast-docker-swarm-discovery-spi'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bitsofinfo/hazelcast-docker-swarm-discovery-spi'
        publicDownloadNumbers = true
        version {
            name = project.property('version')
            desc = project.property('version') + " : " + project.property('description')
            released = new Date()
            vcsTag = project.property('version')
        }
    }
}


publishing {
    publications {
        hazelcastDockerSwarmDiscoverySpi(MavenPublication) {
            from components.java

            groupId project.property('group')
            artifactId 'hazelcast-docker-swarm-discovery-spi'
            version project.property('version')

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().appendNode('name', project.property('group')+":hazelcast-docker-swarm-discovery-spi")
                asNode().appendNode('description', project.property('description'))
                asNode().appendNode('url', "https://github.com/bitsofinfo/hazelcast-docker-swarm-discovery-spi")
                asNode().appendNode('packaging', "jar")
                asNode().appendNode('licenses').appendNode('license')
                    .appendNode('name', "The Apache License, Version 2.0").parent()
                    .appendNode('url', "http://www.apache.org/licenses/LICENSE-2.0.txt")
                asNode().appendNode('developers').appendNode('developer')
                    .appendNode('id', "bitsofinfo").parent()
                    .appendNode('name', "bitsofinfo").parent()
                    .appendNode('email', "bitsofinfo.g@gmail.com").parent()
                    .appendNode('organization',"bitsofinfo").parent()
                    .appendNode('organizationUrl','https://github.com/bitsofinfo')
                asNode().appendNode('scm')
                    .appendNode('connection', "scm:git:https://github.com/bitsofinfo/hazelcast-docker-swarm-discovery-spi.git").parent()
                    .appendNode('developerConnection', "scm:git:https://github.com/bitsofinfo/hazelcast-docker-swarm-discovery-spi.git").parent()
                    .appendNode('url', "https://github.com/bitsofinfo/hazelcast-docker-swarm-discovery-spi")   
            }

        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task runTests(type: Test) {
    description 'Runs unit tests for all tests except TestDoNothingRegistrator.'

    //Always run tests even when up-to-date
    outputs.upToDateWhen {
        false
    }

}

task unitTest(type: Test) {
    description 'Runs unit tests for single class.'

    //Always run tests even when up-to-date
    outputs.upToDateWhen {
        false
    }

}

runTests {
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}

unitTest {
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}



